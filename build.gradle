plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'com.amirali'
version '1.0'
description 'Binary Clock'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.amirali.binaryclock'
    mainClass = 'com.amirali.binaryclock.App'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${project.layout.buildDirectory.get().asFile}/distributions/${project.name}-${project.version}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }

    jpackage {
        installerName = project.name
        imageName = project.name
        vendor = 'Amir Ali Azimi'
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth Â© 2024',
                '--app-version', project.version
        ] as List<String>

        installerType = project.findProperty('installerType')
        // we will pass this from the command line (example: -PinstallerType=msi)
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/com/amirali/binaryclock/icons/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/com/amirali/binaryclock/icons/icon.ico']
        }
        if (['deb', 'rpm'].contains(installerType)) {
            imageOptions += ['--icon', 'src/main/resources/com/amirali/binaryclock/icons/icon.png']
            installerOptions += [
                    '--linux-menu-group', 'Educational',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'azimi6370@gmail.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                    '--linux-rpm-license-type', 'Apache-2.0'
            ]
        }
    }
}

jlinkZip {
    group = 'distribution'
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}